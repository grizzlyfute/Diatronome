plugins {
    id 'com.android.application'
}

android {
    namespace 'org.kalinisa.diatronome'
    compileSdk 35

    defaultConfig {
        applicationId "org.kalinisa.diatronome"
        minSdk 21
        targetSdk 35
        versionCode 4
        versionName "1.0.3"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        vectorDrawables.useSupportLibrary = true
    }

    // Create a @string/versionName in generated
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    // Disable DependencyInfoBlock, signing block added by AGP and
    // encrypted with the Google public key so it can't be read
    // by anyone else except Google.
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    // Workaround https://issuetracker.google.com/issues/351408623
    buildToolsVersion '34.0.0'

    // Do not include any version control source (VCS) information, avoiding error
    // generate_error_reason: NO_SUPPORTED_VCS_FOUND on fdroid lint
    buildTypes {
        release {
            vcsInfo.include false
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation "androidx.preference:preference-ktx:1.2.1"
}

// @SuppressWarnings("deprecation")
allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
    tasks.withType(Sign) {
        // Workaround https://issuetracker.google.com/issues/351408623
        // https://github.com/obfusk/apksigcopier?tab=readme-ov-file#what-about-signatures-made-by-apksigner-from-build-tools--3500-rc1
        // Using apksigner from build-tools version 34 produces APKs verifiable by apksigcopier, but newer versions will fail.
        // Unfortunately, supporting this change in apksigcopier without breaking compatibility with the signatures currently supported would require rather significant changes. Luckily, there are 3 workarounds available:
        // First: use apksigner from build-tools <= 34.0.0 (clearly not ideal).
        // Second: use apksigner sign from build-tools >= 35.0.0-rc1 with the --alignment-preserved option.
        // Third: use zipalign.py --page-size 16 --pad-like-apksigner --replace on the unsigned APK to replace the padding the same way apksigner now does before using apksigcopier.
        options.compilerArgs << "--alignment-preserved"
    }
}
